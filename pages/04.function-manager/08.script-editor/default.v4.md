---
title: 'Script Editor'
date: '04:07 22-08-2023'
---

The Script Editor, as its name suggests, is used to edit ![](/basics/script.png) [Script](/basics/glossary-and-concepts#script) functions.  
It basically consists in a text editor where the user can write a script using a meta-language with a syntax described below.  
The script can be manually modified if you have a full understanding of the language syntax, otherwise a few helper buttons are available on the rightmost side of the editor to speed up and facilitate the script creation/editing.  
Each line of a script will be executed by QLC+ in a sequential order.

### Controls

|     |     |
| --- | --- |
| ![](/basics/player_play.png) | Enable the output and run the Script to test its execution |
| **Script name** | Change the name of the Script. |
| ![](/basics/edit_add.png) | When clicked, this button will display a popup where you can choose the script snippet you want to insert at the current cursor position in the editor.<br><br>![](/basics/function.png) **Start Function**: Opens the [function Selection](../function-selection) dialog to select a Function to be started.  <br>    This operation will automatically add a comment at the end of the line of code, with the QLC+ name of the selected Function.<br>![](/basics/fileclose.png) **Stop Function**: Opens the [function Selection](../function-selection) dialog to select a Function to be stopped. If at the moment of execution of this line of code the selected Function is not running, this instruction will have no effect.  <br>    This operation will automatically add a comment at the end of the line of code, with the QLC+ name of the selected Function.<br>![](/basics/blackout.png) **Blackout**: Opens dialog requesting whether blackout should be turned on or off.<br>![](/basics/fixture.png) **Set Fixture**: Opens the Fixture Channels selection dialog, where you can choose the channels you want to control with the Script. If multiple channels are selected, this operation will add one line of code for each selected channel. By default, the DMX value generated by this operation will be 0 and has to be changed manually with the script text editor.  <br>    This operation will automatically add a comment at the end of the line of code, with the QLC+ name of the selected fixtures and channels.<br>![](/basics/player_play.png) **System Command**: Opens a file dialog to choose the external program/script to run. Please note that the selected file must be executable to be accepted. Once a file has been selected another dialog will ask to enter the program/script arguments. If none are required, just leave the field empty.<br>![](/basics/speed.png) **Wait**: Opens a dialog requesting the time to be waited before the script can execute the next instruction<br>![](/basics/label.png) **Comment**: Opens a dialog requesting the text of the comment to insert at the cursor position in the editor.  <br>    A comment has a C language style appearance: it starts with "//" and everything until the end of the line is then considered a comment<br>![](/basics/other.png) **Random Number**: Opens a dialog requesting the range of values where to perform a randomization of a number. The resulting code snippet will be inserted at the editor current cursor position.  <br>    This can be useful to randomize DMX values set through the setfixture keyword. See below.<br>![](/basics/fileopen.png) **File Path**: Open a file dialog to select a file. The absolute path and the file name will be inserted at the editor current position.  <br>    If a path contains spaces, it will be written within quotes |
| ![](/basics/editcut.png) | Cut the currently selected text in the editor for a later paste |
| ![](/basics/editcopy.png) | Copy the currently selected text in the editor for a later paste |
| ![](/basics/editpaste.png) | Paste a previously cut or copied text at cursor position in the editor |
| ![](/basics/undo.png) | Undo the last performed operation on the editor |
| ![](/basics/check.png) | Check the Script syntax. A popup message will be displayed indicating if the Script is OK or the line numbers where syntax errors have been found. |

### Language syntax

The QLC+ Script meta-language is based on a **keyword:value** model, with some insertions of C language syntax rules.  
Each line of code is parsed by the QLC+ engine and verified to detect the presence of syntax errors.  
Here's a table describing each keyword accepted by the Script engine and its syntax.

#### startfunction
**Type**: keyword<br>
**Description**: starts a QLC+ Function with the given ID<br>
**Syntax**: `startfunction:functionID`<br>
`functionID` is an integer number of the ID assigned by QLC+ to a Function.<br>
Since IDs are not exposed to QLC+ users, in this case it is convenient to use the helper button on the rightmost side of the editor, which also add a comment with the Function name. <br>
Eventually a user will learn the ID of a Function and therefore use it to manually add more code to the script.<br>
**Example**:
```
startfunction:2 // Green scene
```

#### stopfunction
**Type**: keyword<br>
**Description**: stops a running QLC+ Function with the given ID<br>
**Syntax**: `stopfunction:functionID`<br>
`functionID` is an integer number of the ID assigned by QLC+ to a Function. See _startfunction_ description<br>
**Example**:
```
stopfunction:0 // Blue scene
```

#### blackout
**Type**: keyword<br>
**Description**: turns blackout on or off.  <br>
**Syntax**: `blackout:on|off `<br>
`functionID` is an integer number of the ID assigned by QLC+ to a Function. See _startfunction_ description.<br>
**Examples**:
```
blackout:on
blackout:off
```
#### systemcommand
**Type**: keyword  <br>
**Description**: execute a program or a script at the provided absolute path with the (optional) provided arguments.  <br>
**Syntax**:`systemcommand:programPath arg:arg1 arg:arg2 ... arg:argN`<br>
`programPath` is the absolute path of an executable program or script. For example `/usr/bin/vlc` or `C:\\Tools\\myTool.exe`<br>
If the path to an executable contains spaces, it must be written between quotes.  <br>
`_arg1 ... argN` are the arguments to be used when executing _programPath_. If no arguments are needed, then the arg: keywords are not necessary.  <br>
If an argument contains spaces it must be written between quotes.<br>
**Examples**:
```
systemcommand:/usr/bin/vlc arg:-f arg:/home/user/video.mp4 // plays my video with VLC in fullscreen
systemcommand:"C:\\Program Files\\Tools\\My Tool.exe" arg:"D:\\My Files\\My file.txt"
```

#### setfixture
**Type**: keyword  <br>
**Description**: sets a QLC+ Fixture channel to the provided DMX value.  <br>
**Syntax**: `setfixture:fixtureID ch:channelIndex val:DMXValue`<br>
`fixtureID` is an integer number of the ID assigned by QLC+ to a Function.<br>
Since IDs are not exposed to QLC+ users, in this case it is convenient to use the helper button on the rightmost side of the editor, which also add a comment with the fixture and channel name.  <br>
Eventually a user will learn the ID of a fixture and the index of a channel and therefore use them to manually add more code to the script.  <br>
`channelIndex` is an integer number representing the fixture channel number. Channels indices here start from 0.  <br>
`DMXValue` is the actual DMX value to be set to the specified fixture channel. It ranges from 0 to 255  <br>
**Example**:
```
setfixture:0 ch:1 val:135 // Generic RGB, Red. Sets the red channel of a Generic RGB fixture to DMX value 135
```

#### wait
**Type**: keyword  <br>
**Description**: wait for the provided amount of time before executing the next line of code.  <br>Note that a wait time can be randomized too, following the _random_ syntax described below.  <br>
**Syntax**: `wait:time`<br>
`time` can be either an integer number of milliseconds or a string representing the wait time in the QLC+ way: \*\*h\*\*m\*\*s.\*\*  <br>
**Examples**:
```
wait:1800 // Waits for 1 second and 800 milliseconds
wait:03s.20 // Waits for 3 seconds and 200 milliseconds
```

#### waitfunctionstart
**Type**: keyword  <br>
**Description**: wait until a specified function starts running.<br> This command takes one argument, which is the ID of the function you want to wait for. Once the specified function starts running or if it is already running, the script will continue executing from the next line.<br>
**Syntax**: `waitfunctionstart:functionID`<br>
`functionID` is an integer number of the ID assigned by QLC+ to a Function.  See _startfunction_ description.<br>
**Examples**:
```
waitfunctionstart:2 // Wait for green scene to start
```

#### waitfunctionstop
**Type**: keyword  <br>
**Description**: wait until a specified function stops running.<br> This command takes one argument, which is the ID of the function you want to wait for. Once the specified function stops running or if it is not already running, the script will continue executing from the next line.<br>
**Syntax**: `waitfunctionstart:functionID`<br>
`functionID` is an integer number of the ID assigned by QLC+ to a Function.  See _startfunction_ description.<br>
**Examples**:
```
waitfunctionstop:0 // Wait for blue scene to stop
```

#### comments
**Type**: Helper macro  <br>
**Description**: comments can be inserted at any position in the script code and they do not affect the script execution. They are normally used to give a meaning to a line of code.  <br>
QLC+ Scripts comment follow the C Language style rule of the "//" syntax. Basically everything written after "//" will be considered a comment until the end of the line of code.  <br>
So pay a particular attention to not writing meaningful code after a "//" and expect it to be run, cause it won't.  <br>
Comments can be added at the end of a Script line of code or they can take a whole line, for example to describe an entire block of code.

#### random
**Type**: Helper macro  <br>
**Description**: generates a random integer number between the provided minimum and maximum values  <br>
**Syntax**: `random(min,max)`<br>
`min` is the minimum value the randomization can reach. It can be either an integer number or a time string  <br>
`max` is the maximum value the randomization can reach. It can be either an integer number or a time string  <br>
**Examples**:
```
wait:random(02s.00,05s.00) // Waits a random time between 2 and 5 seconds 
// set channel 3 of fixture with ID:1 to a random DMX value between 20 and 235
setfixture:1 ch:2 val:random(20,235)
```

#### stoponexit
**Type**: keyword<br>
**Description**: controls whether functions started during the script should be stopped at the end.<br>
**Syntax**: `stoponexit:[true|false]`<br>
`true`: all functions started afterward will be stopped when the script finishes (default behaviour).<br>
`false`: all functions started afterward will continue running when the script finishes.<br>
**Example**:<br>
In this script example, functions 1 and 3 will be stopped at the end, but functions 2 and 4 will continue running.
```
startfunction:1 // default behaviour: funciton 1 will be stopped

stoponexit:false
startfunction:2 // function 2 will continue running

stoponexit:true
startfunction:3 // function 3 will be stopped

stoponexit:false
startfunction:4 // function 4 will continue running
```
